.class abstract sealed ImportAddressTable {
  .method public static void Write() {
    ldc.i4 0x2270 // TODO(link) HintNameTableRVA
    call void C::Write(int32)

    ldc.i4.0
    call void C::Write(int32)

    ret
  }
}

.class abstract sealed EndingImports {
  .method public static void Write() {
    call void ImportTable::Write()
    call void ImportLookupTable::Write()
    call void ImportAddressHintNameTable::Write()
    call void RuntimeEngineName::Write()

    ldc.i4.0
    call void C::Write(int32)

    call void NativeEntryPoint::Write()
    
    ret
  }
}

.class abstract sealed ImportTable {
  .method public static void Write() {
    ldc.i4 0x2268 // TODO(link) ImportLookupTable RVA
    call void C::Write(int32)

    ldc.i4.0
    call void C::Write(int32) // DateTimeStamp
    ldc.i4.0
    call void C::Write(int32) // ForwarderChain

    ldc.i4 0x227E // TODO(link) Name RVA for "mscoree.dll"
    call void C::Write(int32)

    ldc.i4 0x2000 // TODO(link) ImportAddressTable RVA
    call void C::Write(int32)

    ldc.i4 0x14
    newarr uint8
    call void C::Write(uint8[])

    ret
  }
}

.class abstract sealed ImportLookupTable {
  .method public static void Write() {
    ldc.i4 0x2270 // TODO(link) HintNameTableRVA
    call void C::Write(int32)

    ldc.i4.0
    call void C::Write(int32)

    ret
  }
}


.class abstract sealed ImportAddressHintNameTable {
  .method public static void Write() {
    ldc.i4.0
    call void C::Write(int16)

    ldstr "_CorExeMain"
    call void C::WriteUTF8(string)
    ldc.i4.0
    call void C::Write(uint8)

    ret
  }
}


.class abstract sealed RuntimeEngineName {
  .method public static void Write() {
    ldstr "mscoree.dll"
    call void C::WriteUTF8(string)
    ldc.i4.0
    call void C::Write(uint8)

    ret
  }
}


.class abstract sealed NativeEntryPoint {
  .method public static void Write() {
    ldc.i4.s 0xFF // JMP x86 op
    call void C::Write(uint8)
    ldc.i4.s 0x25 // Mod -- Specifies the jump is in absolute indirect mode
    call void C::Write(uint8)
    
    ldc.i4 0x402000 // Jump target RVA
    call void C::Write(int32)

    ret
  }
}
